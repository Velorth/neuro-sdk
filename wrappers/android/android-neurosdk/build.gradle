tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayRepo = 'neuro-sdk'
    bintrayName = 'neuro-sdk'

    publishedGroupId = 'com.neuromd.neurosdk'
    libraryName = 'NeuroSDK'
    artifact = 'neuro-sdk'

    libraryDescription = 'Base software development kit library for NeuroMD devices'

    siteUrl = 'https://wiki.neuromd.com/'
    gitUrl = 'https://github.com/NeuroMD/neuro-sdk.git'

    libraryVersion = currentSdkVersion

    developerId = 'neuromdltd'
    developerName = 'NeuroMD'
    developerEmail = 'support@neuromd.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName currentSdkVersion
        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task copyLibs(type: Copy) {
    from '../share/libs'
    include 'classes.jar'
    into 'libs'
}

task copyNativeHeaders(type: Copy) {
    from '../share/include'
    include '/**/*'
    into 'src/main/assets'
}

task copyWrapperHeaders(type: Copy) {
    from 'src/main/cpp/include'
    include 'wrappers/jni_ptr_wrap.h'
    include 'wrappers/device/jni_device_wrap.h'
    include 'wrappers/channels/*'
    into 'src/main/assets'
}

preBuild.dependsOn(':neurosdk:build')
preBuild.dependsOn('copyLibs')
preBuild.dependsOn('copyNativeHeaders')
preBuild.dependsOn('copyWrapperHeaders')

archivesBaseName = 'neuro-sdk'
group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = publishUser
    key = publishKey

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = 'neuromdltd'
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}